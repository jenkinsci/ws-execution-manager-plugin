/*
 * Copyright Â© 2018 Worksoft, Inc.
 */

buildscript {
    // These are the list of remote repositories where Gradle Plugins can be found
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    // These are Gradle Plugins this build uses.  See also the plugins block below
    dependencies {
//        classpath 'gradle.plugin.de.fuerstenau:BuildConfigPlugin:1.1.8'
//        classpath 'net.saliman:gradle-properties-plugin:1.4.5'
    }
}

plugins {
    id 'groovy'
    id 'org.jenkins-ci.jpi' version '0.27.0'
    id 'com.github.onslip.gradle-one-jar' version '1.0.5'
}

apply plugin: 'idea'
//apply plugin: 'gradle-one-jar'

// Where to resolve external dependencies.
repositories {
    jcenter()
    maven {
        url('https://repo.jenkins-ci.org/public/')
    }
}

group = 'org.jenkins-ci.plugins'
version = '0.0.2-beta'
description = 'Adds build step for interacting with a Worksoft Execution Manager.'

jenkinsPlugin {
    coreVersion = '2.73.3'
    displayName = "Worksoft Execution Manager Plugin"
    shortName = "ws-execution-manager"
    
    // URL for plugin on Jenkins wiki or elsewhere
    url = 'http://wiki.jenkins-ci.org/display/JENKINS/Worksoft+Execution+Manager+Plugin'
    
    developers {
        developer {
            id "rrinehart"
            name "Raleigh Rinehart"
            email "rrinehart@worksoft.com"
        }
        developer {
            id "dtheobald"
            name "Doug Theobald"
            email "dtheobald@worksoft.com"
        }
        developer {
            id "oullah"
            name "Obaid Ullah"
            email "oullah@worksoft.com"
        }
    }
}

dependencies {
    jenkinsPlugins 'org.jenkins-ci.plugins:credentials:2.1.10'
    jenkinsPlugins 'org.jenkins-ci.plugins:structs:1.17'
    compile 'org.jodd:jodd-http:5.0.1'
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
}


idea{
    module{
        inheritOutputDirs = false
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

task listSrc(group: 'debugging', description: 'list project source') {
    doLast { task ->
        println "Project $task.project.name Source Sets:"

        task.project.sourceSets.each { srcSet ->
            println " - SourceSet: $srcSet.name"
            srcSet.allSource.each { file ->
                println "    - ${file}"
            }
        }
    }
}


task listCompile(group: 'debugging', description: 'show compile configuration') {
    doLast {
        configurations.each { conf ->
            println "Configuration $conf"
            println "              $conf.allDependencies"
        }
    }
}
sourceCompatibility = 1.8
targetCompatibility = 1.8
